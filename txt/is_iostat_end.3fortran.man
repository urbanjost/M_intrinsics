NAME

IS_IOSTAT_END(3) - [STATE] Test for end-of-file value

SYNTAX

    function is_iostat_end(i)

        logical function   :: is_iostat_end (i) result(yesno)
        integer,intent(in) :: i

DESCRIPTION

is_iostat_end(3) tests whether a variable (assumed returned as a status
from an I/O statement) has the "end of file" I/O status value.

The function is equivalent to comparing the variable with the IOSTAT_END
parameter of the intrinsic module ISO_FORTRAN_ENV.

ARGUMENTS

-   I : An _integer_ status value to test if indicating end of file.

RETURNS

Returns a _logical_ of the default kind, .TRUE. if I has the value which
indicates an end of file condition for IOSTAT= specifiers, and is
.FALSE. otherwise.

EXAMPLES

Sample program:

    program demo_iostat
    implicit none
    real               :: value
    integer            :: ios
    character(len=256) :: message
       write(*,*)'Begin entering numeric values, one per line'
       do
          read(*,*,iostat=ios,iomsg=message)value
          if(ios.eq.0)then
             write(*,*)'VALUE=',value
          elseif( is_iostat_end(ios) ) then
             stop 'end of file. Goodbye!'
          else
             write(*,*)'ERROR:',ios,trim(message)
          endif
          !
       enddo
    end program demo_iostat

STANDARD

Fortran 2003 and later

fortran-lang intrinsic descriptions (license: MIT) @urbanjost

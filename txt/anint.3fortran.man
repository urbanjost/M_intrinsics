NAME

ANINT(3) - [NUMERIC] Nearest whole number

SYNTAX

    result = anint(a, kind)

DESCRIPTION

ANINT(A [, KIND]) rounds its argument to the nearest whole number.

ARGUMENTS

-   

    A
        the type of the argument shall be real.

-   

    KIND
        (optional) an integer initialization expression indicating the
        kind parameter of the result.

RETURNS

The return value is of type real with the kind type parameter of the
argument if the optional KIND is absent; otherwise, the kind type
parameter will be given by KIND. If A is greater than zero, ANINT(A)
returns AINT(A + 0.5). If A is less than or equal to zero then it
returns AINT(A - 0.5).

EXAMPLES

Sample program:

    program demo_anint
    use, intrinsic :: iso_fortran_env, only : real_kinds, &
    & real32, real64, real128
    implicit none
    real(kind=real32) :: x4
    real(kind=real64) :: x8

       x4 = 1.234E0_real32
       x8 = 4.321_real64
       print *, anint(x4), dnint(x8)
       x8 = anint(x4,kind=real64)
       print *, x8
       print *
       ! elemental
       print *,anint([ &
        & -2.7,  -2.5, -2.2, -2.0, -1.5, -1.0, -0.5, &
        &  0.0, &
        & +0.5,  +1.0, +1.5, +2.0, +2.2, +2.5, +2.7  ])

    end program demo_anint

Results:

        1.00000000       4.0000000000000000
        1.0000000000000000

       -3.00000000      -3.00000000      -2.00000000      -2.00000000
       -2.00000000      -1.00000000      -1.00000000       0.00000000
        1.00000000       1.00000000       2.00000000       2.00000000
        2.00000000       3.00000000       3.00000000

STANDARD

FORTRAN 77 and later

SEE ALSO

AINT(3), INT(3), NINT(3), SELECTED_INT_KIND(3), CEILING(3), FLOOR(3)

fortran-lang intrinsic descriptions

NAME

SINH(3) - [MATHEMATICS:TRIGONOMETRIC] Hyperbolic sine function

SYNTAX

    result = sinh(x)

        elemental TYPE(kind=KIND) function sinh(x)
        TYPE(kind=KIND) :: x

Where the returned value has the kind of the input value and TYPE may be
real or complex

DESCRIPTION

SINH(X) computes the hyperbolic sine of X.

The hyperbolic sine of x is defined mathematically as:

SINH(X) = (EXP(X) - EXP(-X)) / 2.0

If X is of type complex its imaginary part is regarded as a value in
radians.

ARGUMENTS

-   X : The type shall be real or complex.

RETURNS

The return value has same type and kind as X.

EXAMPLES

Sample program:

    program demo_sinh
    use, intrinsic :: iso_fortran_env, only : &
    & real_kinds, real32, real64, real128
    implicit none
    real(kind=real64) :: x = - 1.0_real64
    real(kind=real64) :: nan, inf
    character(len=20) :: line

       print *, sinh(x)
       print *, (exp(x)-exp(-x))/2.0

       ! sinh(3) is elemental and can handle an array
       print *, sinh([x,2.0*x,x/3.0])

       ! a NaN input returns NaN
       line='NAN'
       read(line,*) nan
       print *, sinh(nan)

       ! a Inf input returns Inf
       line='Infinity'
       read(line,*) inf
       print *, sinh(inf)

       ! an overflow returns Inf
       x=huge(0.0d0)
       print *, sinh(x)

    end program demo_sinh

Results:

      -1.1752011936438014
      -1.1752011936438014
      -1.1752011936438014       -3.6268604078470190      -0.33954055725615012
                           NaN
                      Infinity
                      Infinity

STANDARD

Fortran 95 and later, for a complex argument Fortran 2008 or later

SEE ALSO

-   Wikipedia:hyperbolic functions

ASINH(3)

fortran-lang intrinsic descriptions

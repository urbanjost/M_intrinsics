NAME

ICHAR(3) - [CHARACTER:CONVERSION] Character-to-integer conversion
function

SYNTAX

       elemental function ichar(c,kind)

        character(len=1),intent(in) :: c
        integer,intent(in),optional :: kind

DESCRIPTION

ICHAR(C) returns the code for the character in the system's native
character set. The correspondence between characters and their codes is
not necessarily the same across different Fortran implementations. For
example, a platform using EBCDIC would return different values than an
ASCII platform.

See IACHAR(3) for specifically working with the ASCII character set.

ARGUMENTS

-   C : Shall be a scalar character, with INTENT(IN)

-   KIND : (Optional) An integer initialization expression indicating
    the kind parameter of the result.

RETURNS

The return value is of type integer and of kind KIND. If KIND is absent,
the return value is of default integer kind.

EXAMPLES

Sample program:

    program demo_ichar
    implicit none
    integer i

       write(*,*)ichar(['a','z','A','Z'])
       do i=0,127
          call printme()
       enddo

    contains

       subroutine printme()
       character(len=1) :: letter

          letter=char(i)
          select case(i)
          case (:31,127:)
             write(*,'(1x,i0.3,1x,"HEX=",z2.2,1x,i0)')i,letter,ichar(letter)
          case default
             write(*,'(1x,i0.3,1x,a,1x,i0)')i,letter,ichar(letter)
          end select

       end subroutine printme

    end program demo_ichar

NOTE

No intrinsic exists to convert between a numeric value and a formatted
character string representation -- for instance, given the character
value '154', obtaining an integer or real value with the value 154, or
vice versa. Instead, this functionality is provided by internal-file
I/O, as in the following example:

    program read_val
    integer value
    character(len=10) string, string2
       string = '154'

       ! Convert a string to a numeric value
       read (string,'(I10)') value
       print *, value

       ! Convert a value to a formatted string
       write (string2,'(I10)') value
       print *, string2
    end program read_val

Results:

                154
               154

STANDARD

Fortran 95 and later, with KIND argument -Fortran 2003 and later

SEE ALSO

ACHAR(3), CHAR(3), IACHAR(3)

Functions that perform operations on character strings, return lengths
of arguments, and search for certain arguments:

-   ELEMENTAL: ADJUSTL(3), ADJUSTR(3), INDEX(3),

SCAN(3), VERIFY(3)

-   NONELEMENTAL: LEN_TRIM(3), LEN(3), REPEAT(3), TRIM(3)

fortran-lang intrinsic descriptions
